/*
 * Cryptonose2
 *
 * Copyright Â© 2019-2020 Dawid Motyka
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id "org.beryx.jlink" version "2.21.2"
}

group 'pl.dmotyka'
version '2.9.7'
description 'Real-time alerts about significant price changes on cryptocurrency exchanges'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'pl.dmotyka', name: 'dmutils', version:'1.0.0'
    compile group: 'pl.dmotyka', name: 'exchangeutils', version:'1.5.1'
    compile group: 'pl.dmotyka', name: 'cryptonoseengine', version:'1.3.1'
    compile group: 'com.dorkbox', name: 'Notify', version:'3.7'
}

javafx {
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml']
}

mainClassName = "pl.dmotyka.cryptonose2.AppStarter"

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Cryptonose'
    }
    forceMerge('log4j-api')

    jpackage {
        // Set the environment property BADASS_JLINK_JPACKAGE_HOME or explicitly configure the below property
        // jpackageHome = '/usr/lib/jvm/adoptopenjdk-14-openj9-amd64'
        installerOptions += [
                '--description', project.description,
                '--app-version', version,
                '--copyright', 'Copyrigtht 2020 Dawid Motyka',
                '--license-file', 'LICENSE.txt',
                '--vendor', 'Dawid Motyka'
        ]
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            imageOptions += ['--icon', 'src/main/resources/launcher.png']
            installerOptions += [
                    '--linux-menu-group', 'Internet',
                    '--description', project.description,
                    '--app-version', version,
                    '--linux-shortcut',
                    '--linux-deb-maintainer', 'cryptonose2@gmail.com',
                    '--linux-rpm-license-type', 'MIT'
            ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            imageOptions += ['--icon', 'src/main/resources/launcher.ico']
            installerOptions += ['--win-per-user-install',
                                 '--win-dir-chooser',
                                 '--win-menu',
                                 '--win-shortcut',]
        }
    }
}
