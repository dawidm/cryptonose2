/*
 * Cryptonose
 *
 * Copyright Â© 2019-2021 Dawid Motyka
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 */

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id "org.beryx.jlink" version "2.23.6"
}

group 'pl.dmotyka'
version '2.20.0'
description 'Real-time alerts about significant price changes on cryptocurrency exchanges'

java {
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

application {
    mainModule = "cryptonose"
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'pl.dmotyka:dmutils:1.1.0'
    implementation ('pl.dmotyka:exchangeutils:1.15.9')  {
        exclude group: 'javax.annotation', module: 'javax.annotation-api' // conflict with jsr305
        exclude group: 'jakarta.ws.rs', module: 'jakarta.ws.rs-api' // conflict with javax.ws.rs
    }
    implementation ('pl.dmotyka:cryptonoseengine:1.9.2')  {
        exclude group: 'javax.annotation', module: 'javax.annotation-api' // conflict with jsr305
        exclude group: 'jakarta.ws.rs', module: 'jakarta.ws.rs-api' // conflict with javax.ws.rs
    }
    implementation 'pl.dmotyka:minimal-fx-charts:1.1.0'
    implementation 'com.dorkbox:Notify:3.7'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome-pack:12.2.0'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.2.0'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1'
    //runtime fileTree(dir: 'plugins', include: '*.jar')
}

javafx {
    version = "15.0.1"
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml']
}

mainClassName = "pl.dmotyka.cryptonose2.AppStarter"

run {
    jvmArgs = ['-Dsun.java2d.uiScale=1.0', '-Djava.util.logging.config.file=logging-debug.settings', '-Dversion='+project.version]
}

jlink {
    options = ['--strip-debug', '--compress', '0', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Cryptonose'
        jvmArgs = ['-Dsun.java2d.uiScale=1.0', '-Dversion='+project.version]
    }
    forceMerge('log4j-api')

    jpackage {
        // Set the environment property BADASS_JLINK_JPACKAGE_HOME or explicitly configure the below property
        // jpackageHome = '/usr/lib/jvm/adoptopenjdk-14-openj9-amd64'
        installerOptions += [
                '--description', project.description,
                '--app-version', version,
                '--copyright', 'Copyrigtht 2020 Dawid Motyka',
                '--license-file', 'LICENSE.txt',
                '--vendor', 'Dawid Motyka'
        ]
        if (org.gradle.internal.os.OperatingSystem.current().linux) {
            imageOptions += ['--icon', 'src/main/resources/launcher.png']
            installerOptions += [
                    '--resource-dir', "build/jpackage/Cryptonose/lib",
                    '--linux-menu-group', 'Network',
                    '--description', project.description,
                    '--app-version', version,
                    '--linux-shortcut',
                    '--linux-deb-maintainer', 'cryptonose2@gmail.com',
                    '--linux-rpm-license-type', 'MIT'
            ]
        }
        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            imageOptions += ['--icon', 'src/main/resources/launcher.ico']
            installerOptions += ['--win-per-user-install',
                                 '--win-dir-chooser',
                                 '--win-menu',
                                 '--win-shortcut',
                                 '--win-menu-group', 'Cryptonose']
        }
    }
}

task packZipLinux(type: Exec) {
    commandLine 'bash', './pack_zip.sh', 'cryptonose-' + version + '-' + org.gradle.internal.os.OperatingSystem.current().name + '-x86_64' + '.zip'
}

task packZip {
    if (org.gradle.internal.os.OperatingSystem.current().linux) {
        dependsOn(packZipLinux)
    }
}

jpackage.finalizedBy packZip
